# Reloads files under profile.d

emulate -L zsh

setopt extendedglob

local -r usage=(
	"Usage: ${funcstack[1]} [OPTION...] [FILE...] [DIR...]"
	"Extension to shell built-in command 'source'."
	"Presumes \$ZDOTDIR/profile.d as default prefix."
	""
	"\t[-h|--help] : Print this help message"
	"\t[-v] / [-q] : Increase / Decrease verbosity"
	"\t[-p|--prefix] : Set PREFIX directory for profile search. Default: $ZDOTDIR/profile.d"
	"\t[-a|--all] : Reload all files under PREFIX. Supercedes any of the following options"
	"\t[-l|--login] : Reloads all files containing block for [[ -o login ]]. Stackable option"
	"\t[-i|--interactive] : Reloads all files containing block for [[ -o interactive ]]. Stackable option"
)

## Setup parseopts (with no extra arguments)
local void f_help f_verbosity
local f_pfx f_all f_opts
zparseopts -a void -D -F -K -- \
	{h,-help}=f_help \
	v+=f_verbosity q+=f_verbosity \
	{p,-prefix}:=f_pfx \
	{a,-all}=f_all \
	{l,-login}=f_opts \
	{i,-interactive}=f_opts \
	|| return 1

## Help/usage message
if [[ "$f_help" ]]; then
	>&2 print -l $usage
	[[ "$f_help" ]]; return $?
fi

### Arg parsing
# Verbosity
local -i verbosity=0
f_verbosity="${(j::)f_verbosity//-}"
(( verbosity += (${#f_verbosity//q} - ${#${f_verbosity//v}}) ))

# To check if there was an intended use of this command
local -r flags_set="${f_pfx}${f_all}${f_opts}"

# Prefix (last one set)
f_pfx="${f_pfx:+${f_pfx[-1]:P}}"
# If no prefix set, default to profile.d
if [[ -z "$f_pfx" ]]; then
	f_pfx="$ZDOTDIR/profile.d"
else
	local err_pfx="Cannot use prefix '$f_pfx'"
	# If prefix directory not found, abort
	if [[ ! -d "$f_pfx" ]]; then
		print_fn -e "%s: %s" "$err_pfx" "Directory not found"
		return 1
	# If given directory is not readable by current user, abort
	elif [[ ! -r "$f_pfx" ]]; then
		print_fn -e "%s: %s" "$err_pfx" "Directory not readable"
		return 1
	fi
fi

# Prepare profiles to load
local -aU profiles=()

# Load all profiles if specified
if [[ "$f_all" ]]; then
	profiles=( "$f_pfx"/{.*,^.}/**/*.zsh(-DN.) )
else
	# Load login/interactive profiles if specified
	(( ${f_opts[(I)-i]} )) && profiles+=( $(\grep -rEl '\[?\[ -o login \]\]?' "$f_pfx") )
	(( ${f_opts[(I)-l]} )) && profiles+=( $(\grep -rEl '\[?\[ -o interactive \]\]?' "$f_pfx") )

	# Load any potential match from arguments, file or directory
	(( $# )) && profiles+=( "$f_pfx"/**/(${(j:|:)@}){,/**/*}.zsh(-DN.) )
fi


# If no profiles were set, do nothing
if (( ! ${#profiles} )); then
	if (( 1 <= $verbosity )); then
		if [[ "$flags_set" ]]; then
			print_fn -e "No valid profiles found with the specified flags"
		else
			print_fn -i "No profiles to source"
		fi
	fi
	[[ "$flags_set" ]]
	return $?
fi

# Shorten paths just for output
if (( 1 <= $verbosity )); then
	local s_profiles=(${profiles//"${ZDOTDIR}"/\$ZDOTDIR})
	(( 1 == $verbosity )) && s_profiles=(${s_profiles//"\$ZDOTDIR\/profile.d\/"})
	(( 2 <= $verbosity )) && printf "%s\n" "Sourcing the following profiles:"
	if (( ${#s_profiles} )); then
		(( 1 <= $verbosity )) && printf "%s\n" ${s_profiles}
	else
		(( 1 <= $verbosity )) && printf "%s\n" ${(j:, :)s_profiles}
	fi
fi

# Source resulting profiles
local arg
for arg in $profiles; do
	if ! source "$arg"; then
		(( 1 <= $verbosity )) && print_fn -e "error when sourcing '${arg//"${ZDOTDIR}"/\$ZDOTDIR}'"
		return 1
	fi
done
