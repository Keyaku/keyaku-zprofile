# Checks argument list for valid commands.
# Can make use of operators AND and OR.

emulate -L zsh

local -r usage=(
	"Usage: $(get_funcname) [OPTION...] COMMAND..."
	"\t[-h|--help] : Print this help message"
	"\t[-v] / [-q] : Increase / Decrease verbosity"
	"\t[-o|--or] : Logical OR operator. Default behavior. Checks if any of the commands are valid."
	"\t[-a|--and] : Logical AND operator. Checks if all of the commands are valid."
)

## Setup func opts
## FIXME: add flag for ignoring if command is alias
local f_help f_verbose f_quiet f_exit logical=or # default
zparseopts -D -F -K -- \
	{h,-help}=f_help \
	v+=f_verbose q+=f_quiet \
	{o,-or}=logical \
	{a,-and}=logical \
	|| return 1

## Help/usage message
if (( ! $# )) || [[ "$f_help" ]]; then
	>&2 print -l $usage
	[[ "$f_help" ]]; return $?
fi

## Arg parsing
local verbosity=0
(( verbosity += ($#f_verbose - $#f_quiet) ))
logical=${logical##*-}

## function logic
local -aU valid=(${(k)commands[(I)${(j:|:)@}]} ${(k)functions[(I)${(j:|:)@}]})
local -aU invalid=(${@:|valid})

## Print non-valid commands
if (( $verbosity )) && (( ${#invalid} )); then
	>&2 echo "Invalid commands:"
	>&2 echo "${invalid}"
fi
{ (( ! ${#invalid} )) && [[ "$logical" =~ a(nd)? ]] } ||
{ (( ${#valid} )) && [[ "$logical" =~ o(r)? ]] }
