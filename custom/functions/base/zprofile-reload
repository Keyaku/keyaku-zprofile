# Reloads files under profile.d

emulate -L zsh

setopt extendedglob

local usage=(
	"Usage: $(get_funcname) [OPTION...] [FILE...] [DIR...]"
	"\t[-h|--help] : Print this help message"
	"\t[-v] / [-q] : Increase / Decrease verbosity"
	"\t[-f|--file] FILE : Reloads the specified file(s) under profile.d"
	"\t[-d|--dir] DIR : Reloads all files under the specified directory(ies) under profile.d"
)

## Setup parseopts (with no extra arguments)
local void f_help f_verbose f_quiet f_login f_interactive
local -aU f_dirs f_files
zparseopts -a void -D -F -K -- \
	{h,-help}=f_help \
	v+=f_verbose q+=f_quiet \
	{d,-dir}+:=f_dirs \
	{f,-file}+:=f_files \
	|| return 1

## Help/usage message
if [[ "$f_help" ]]; then
	>&2 print -l $usage
	[[ "$f_help" ]]; return $?
fi

## Arg parsing
local verbosity=0
(( verbosity += ($#f_verbose - $#f_quiet) ))
logical=${logical##*-}

# Prepare profiles to load
local -a flags_to_unset=(-f --file -d --dir)

f_files=(${${f_files:|flags_to_unset}:s/.zsh/})
f_dirs=(${f_dirs:|flags_to_unset})

local -aU profiles=()

# Load specified files/dirs from arguments
if [[ "$f_files" ]]; then
	profiles+=( "$ZDOTDIR"/profile.d/{.*,^.}/**/((${(j:|:)~f_files}).zsh)(-DN.) )
fi
if [[ "$f_dirs" ]]; then
	profiles+=( "$ZDOTDIR"/profile.d/**/(${(j:|:)~f_dirs})/**/*.zsh(-DN.) )
fi

# Load any potential match from arguments, file or directory
if (( $# )); then
	profiles+=( "$ZDOTDIR"/profile.d/**/(${(j:|:)@}){,/**/*}.zsh(-DN.) )
# Load all profiles if none specified
elif [[ -z "${f_files}${f_dirs}" ]] && (( ! ${#profiles} )); then
	profiles=( "$ZDOTDIR"/profile.d/{.*,^.}/**/*.zsh(-DN.) )
fi

# Final check for valid profiles
if (( ! ${#profiles} )); then
	>&2 echo "No valid files or directories specified"
	return 1
fi

# Shorten paths just for output
if (( 2 <= $verbosity )); then
	local s_profiles=(${profiles//"${ZDOTDIR}"/\$ZDOTDIR})
	echo "Sourcing the following profiles:"
	if (( ${#s_profiles} )); then
		printf "  %s\n" ${s_profiles}
	else
		printf "  %s\n" ${(j:, :)s_profiles}
	fi
fi

# Source resulting profiles
set -- $profiles
while (( $# )); do
	if ! source "$1"; then
		(( 1 <= $verbosity )) && print_fn -e "error when sourcing '${1//"${ZDOTDIR}"/\$ZDOTDIR}'"
		return 1
	fi
	shift
done
