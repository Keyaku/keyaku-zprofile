# Loads all custom functions

emulate -L zsh
setopt extendedglob

## Setup func opts
local f_help f_reload
zparseopts -D -F -K -- \
	{r,-reload}=f_reload \
	|| return 1

[[ "$ZSH_CUSTOM" ]] || ZSH_CUSTOM="$ZDOTDIR/custom"

# Add all non-empty subdirectories of custom functions to fpath after $ZSH_CUSTOM/functions, or at the beginning
if ! [[ " $fpath " =~ "$ZSH_CUSTOM"/functions/.\* ]]; then
	local -i idx=${fpath[(i)$ZSH_CUSTOM/functions]}
	(( ${#fpath} < idx )) && idx=0
	fpath[$idx+1,0]=("$ZSH_CUSTOM"/functions/{.,^.}**{,/**}(-/FDN))
fi

local -aU custom_funcs=( "$ZSH_CUSTOM"/functions/{.,^.}/**/*(-.DN:t) )

# Reload all functions
if [[ "$f_reload" ]]; then
	# Unload defined functions first
	unfunction ${(k)functions:*custom_funcs}
fi

# Load custom functions, sorted alphabetically, dotdirectories first
autoload -Uz ${custom_funcs}
